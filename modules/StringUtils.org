# SPDX-FileCopyrightText: © 2024 Alexander Kromm <mmaulwurff@gmail.com>
# SPDX-License-Identifier: BSD-3-Clause
#+property: header-args :comments no :mkdirp yes :noweb yes :results none

#+title: StringUtils

[[file:../documentation/WhereAreTheProjectFiles.org][Where are the project files?]]

* Description

License: [[file:../LICENSES/BSD-3-Clause.txt][BSD-3-Clause]]

#+begin_src c :tangle ../build/StringUtils/StringUtils.zs
// SPDX-FileCopyrightText: © 2024 Alexander Kromm <mmaulwurff@gmail.com>
// SPDX-License-Identifier: BSD-3-Clause

// StringUtils module version: 1.2.0
// StringUtils is a part of DoomToolbox: https://github.com/mmaulwurff/doom-toolbox/
#+end_src

* Classes

** ~StringUtils~

This class acts as a namespace for free functions.

#+begin_src c :tangle ../build/StringUtils/StringUtils.zs :noweb-prefix no

class NAMESPACE_su { <<StringUtilsBody>> }
#+end_src

*** ~join~

Joins strings to a single string separated by delimiter.

#+begin_src c :noweb-ref StringUtilsBody

static clearscope string join(Array<string> strings, string delimiter = ", ")
{
  uint nStrings = strings.size();
  if (nStrings == 0) return "";

  string result = strings[0];

  for (uint i = 1; i < nStrings; ++i)
    result.appendFormat("%s%s", delimiter, strings[i]);

  return result;
}
#+end_src
#+begin_src c :noweb-ref TestsBody
{
  Array<string> strings;
  it("join: empty", Assert(su.join(strings, ".") == ""));

  strings.push("hello");
  it("join: one", Assert(su.join(strings) == "hello"));

  strings.push("world");
  it("join: default delimiter", Assert(su.join(strings) == "hello, world"));
  it("join: empty delimiter",   Assert(su.join(strings, "") == "helloworld"));
}
#+end_src

*** ~repeat~

Repeats the specified string the specified number of times.

#+begin_src c :noweb-ref StringUtilsBody

static clearscope string repeat(string aString, int times)
{
  // Make the specified number of spaces using padding format.
  string result = string.format("%*d", times + 1, 0);
  result.deleteLastCharacter();
  result.replace(" ", aString);
  return result;
}
#+end_src
#+begin_src c :noweb-ref TestsBody
{
  it("repeat: zero",  Assert(su.repeat("a",     0) == ""));
  it("repeat: one",   Assert(su.repeat("hello", 1) == "hello"));
  it("repeat: 3",     Assert(su.repeat("!?",    3) == "!?!?!?"));
  it("repeat: empty", Assert(su.repeat("",      7) == ""));
}
#+end_src

*** ~boolToString~

Writes out a boolean value.

#+begin_src c :noweb-ref StringUtilsBody

static clearscope string boolToString(bool value)
{
  return value ? "true" : "false";
}
#+end_src
#+begin_src c :noweb-ref TestsBody
{
  it("boolToString: true",  Assert(su.boolToString(true)  == "true"));
  it("boolToString: false", Assert(su.boolToString(false) == "false"));
}
#+end_src

** ~Description~

#+begin_src c :tangle ../build/StringUtils/StringUtils.zs

class NAMESPACE_Description
{
  <<DescriptionBody>>
  private Array<string> mFields;
}
#+end_src

Optimization opportunity: add... functions append to a private string, not an array.
Compose simply returns it.

*** ~compose~

#+begin_src c :noweb-ref DescriptionBody

string compose() { return NAMESPACE_su.join(mFields); }
#+end_src
#+begin_src c :noweb-ref TestsBody
{
  let d = new("Description");
  it("description: empty", Assert(d.compose() == ""));
}
#+end_src

*** ~add~

#+begin_src c :noweb-ref DescriptionBody

NAMESPACE_Description add(string name, string value)
{
  mFields.push(name .. ": " .. value);
  return self;
}
#+end_src
#+begin_src c :noweb-ref TestsBody
{
  let d = new("Description");
  d.add("k1", "v1").add("k2", "v2");
  it("description: two", Assert(d.compose() == "k1: v1, k2: v2"));
}
#+end_src

*** ~addObject~

#+begin_src c :noweb-ref DescriptionBody

NAMESPACE_Description addObject(string name, Object anObject)
{
  if (anObject == NULL) return add(name, "NULL");

  string className = anObject.getClassName();
  return add(name, className);
}
#+end_src
#+begin_src c :noweb-ref TestsBody
{
  let d = new("Description");
  Object o;
  d.addObject("n", o).addObject("self", self);
  it("description: object", Assert(d.compose() == "n: NULL, self: su_Test"));
}
#+end_src

*** ~addClass~

#+begin_src c :noweb-ref DescriptionBody

NAMESPACE_Description addClass(string name, Class aClass)
{
  if (aClass == NULL) return add(name, "NULL");
  return add(name, aClass.getClassName());
}
#+end_src
#+begin_src c :noweb-ref TestsBody
{
  string result = new("Description").addClass("c", self.getClass()).compose();
  it("description: class", Assert(result == "c: su_Test"));
}
#+end_src

*** ~addBool~

#+begin_src c :noweb-ref DescriptionBody

NAMESPACE_Description addBool(string name, bool value)
{
  return add(name, NAMESPACE_su.boolToString(value));
}
#+end_src
#+begin_src c :noweb-ref TestsBody
{
  let d = new("Description");
  d.addBool("b", true);
  it("description: bool", Assert(d.compose() == "b: true"));
}
#+end_src

*** ~addInt~

#+begin_src c :noweb-ref DescriptionBody

NAMESPACE_Description addInt(string name, int value)
{
  return add(name, string.format("%d", value));
}
#+end_src
#+begin_src c :noweb-ref TestsBody
{
  let d = new("Description");
  d.addInt("value", -19);
  it("description: int", Assert(d.compose() == "value: -19"));
}
#+end_src

*** ~addFloat~

#+begin_src c :noweb-ref DescriptionBody

NAMESPACE_Description addFloat(string name, double value)
{
  return add(name, string.format("%.2f", value));
}
#+end_src
#+begin_src c :noweb-ref TestsBody
{
  let d = new("Description");
  d.addFloat("value", -19.4);
  it("description: float", Assert(d.compose() == "value: -19.40"));
}
#+end_src

*** ~addDamageFlags~

#+begin_src c :noweb-ref DescriptionBody

NAMESPACE_Description addDamageFlags(string name, EDmgFlags flags)
{
  Array<string> results;
  if (flags & DMG_NO_ARMOR)          results.push("DMG_NO_ARMOR");
  if (flags & DMG_INFLICTOR_IS_PUFF) results.push("DMG_INFLICTOR_IS_PUFF");
  if (flags & DMG_THRUSTLESS)        results.push("DMG_THRUSTLESS");
  if (flags & DMG_FORCED)            results.push("DMG_FORCED");
  if (flags & DMG_NO_FACTOR)         results.push("DMG_NO_FACTOR");
  if (flags & DMG_PLAYERATTACK)      results.push("DMG_PLAYERATTACK");
  if (flags & DMG_FOILINVUL)         results.push("DMG_FOILINVUL");
  if (flags & DMG_FOILBUDDHA)        results.push("DMG_FOILBUDDHA");
  if (flags & DMG_NO_PROTECT)        results.push("DMG_NO_PROTECT");
  if (flags & DMG_USEANGLE)          results.push("DMG_USEANGLE");
  if (flags & DMG_NO_PAIN)           results.push("DMG_NO_PAIN");
  if (flags & DMG_EXPLOSION)         results.push("DMG_EXPLOSION");
  if (flags & DMG_NO_ENHANCE)        results.push("DMG_NO_ENHANCE");

  return add(name, NAMESPACE_su.join(results));
}
#+end_src
#+begin_src c :noweb-ref TestsBody
{
  let d = new("Description");
  d.addDamageFlags("d", DMG_NO_ARMOR | DMG_NO_ENHANCE);
  it("description: damage", Assert(d.compose() == "d: DMG_NO_ARMOR, DMG_NO_ENHANCE"));
}
#+end_src

*** ~addCvar~

#+begin_src c :noweb-ref DescriptionBody

NAMESPACE_Description addCvar(string name)
{
  let aCvar = Cvar.getCvar(name, players[consolePlayer]);
  if (aCvar == NULL) return add(name, "NULL");

  switch (aCvar.getRealType())
    {
    case Cvar.CVAR_Bool: return addBool(name, NAMESPACE_su.boolToString(aCvar.getInt()));
    case Cvar.CVAR_Int: return addInt(name, aCvar.getInt());
    case Cvar.CVAR_Float: return addFloat(name, aCvar.getFloat());
    case Cvar.CVAR_String: return add(name, aCvar.getString());
      // TODO: implement color:
    case Cvar.CVAR_Color: return addInt(name, aCvar.getInt());
    }

  return add(name, string.format("unknown type (%d)", aCvar.getRealType()));
}
#+end_src
#+begin_src c :noweb-ref TestsBody
{
  let d = new("Description");
  d.addCvar("autoaim").addCvar("dmflags2");
  it("description: cvar", Assert(d.compose() == "autoaim: 35.00, dmflags2: 0"));
}
#+end_src

*** ~addSpac~

#+begin_src c :noweb-ref DescriptionBody

/// SPAC - special activation types.
NAMESPACE_Description addSpac(string name, int flags)
{
  Array<string> results;
  if (flags & SPAC_Cross)      results.push("SPAC_Cross");
  if (flags & SPAC_Use)        results.push("SPAC_Use");
  if (flags & SPAC_MCross)     results.push("SPAC_MCross");
  if (flags & SPAC_Impact)     results.push("SPAC_Impact");
  if (flags & SPAC_Push)       results.push("SPAC_Push");
  if (flags & SPAC_PCross)     results.push("SPAC_PCross");
  if (flags & SPAC_UseThrough) results.push("SPAC_UseThrough");
  if (flags & SPAC_AnyCross)   results.push("SPAC_AnyCross");
  if (flags & SPAC_MUse)       results.push("SPAC_MUse");
  if (flags & SPAC_MPush)      results.push("SPAC_MPush");
  if (flags & SPAC_UseBack)    results.push("SPAC_UseBack");
  if (flags & SPAC_Damage)     results.push("SPAC_Damage");
  if (flags & SPAC_Death)      results.push("SPAC_Death");

  return add(name, NAMESPACE_su.join(results));
}
#+end_src
#+begin_src c :noweb-ref TestsBody
{
  let d = new("Description");
  d.addSpac("s", SPAC_Cross | SPAC_Death);
  it("description: SPAC", Assert(d.compose() == "s: SPAC_Cross, SPAC_Death"));
}
#+end_src

*** ~addLine~

#+begin_src c :noweb-ref DescriptionBody

NAMESPACE_Description addLine(string name, Line aLine)
{
  return addInt(name, aLine.index());
}
#+end_src
#+begin_src c :noweb-ref TestsBody
{
  let d = new("Description");
  d.addLine("l", level.lines[1]);
  it("description: line", Assert(d.compose() == "l: 1"));
}
#+end_src

*** ~addSectorPart~

#+begin_src c :noweb-ref DescriptionBody

NAMESPACE_Description addSectorPart(string name, SectorPart part)
{
  switch (part)
    {
    case SECPART_None:    return add(name, "SECPART_None");
    case SECPART_Floor:   return add(name, "SECPART_Floor");
    case SECPART_Ceiling: return add(name, "SECPART_Ceiling");
    case SECPART_3D:      return add(name, "SECPART_3D");
    }

  return add(name, string.format("unknown SECPART (%d)", part));
}
#+end_src
#+begin_src c :noweb-ref TestsBody
{
  let d = new("Description");
  d.addSectorPart("s", SECPART_3D);
  it("description: SECPART", Assert(d.compose() == "s: SECPART_3D"));
}
#+end_src

*** ~addSector~

#+begin_src c :noweb-ref DescriptionBody

NAMESPACE_Description addSector(string name, Sector aSector)
{
  return addInt(name, aSector.index());
}
#+end_src
#+begin_src c :noweb-ref TestsBody
{
  let d = new("Description");
  d.addSector("s", level.sectors[1]);
  it("description: sector", Assert(d.compose() == "s: 1"));
}
#+end_src

*** ~addVector3~

#+begin_src c :noweb-ref DescriptionBody

NAMESPACE_Description addVector3(string name, vector3 vector)
{
  return add(name, string.format("%.2f, %.2f, %.2f", vector.x, vector.y, vector.z));
}
#+end_src
#+begin_src c :noweb-ref TestsBody
{
  let d = new("Description");
  vector3 v = (1.1, 2.2, 3.3);
  d.addVector3("v", v);
  it("description: vector", Assert(d.compose() == "v: 1.10, 2.20, 3.30"));
}
#+end_src

*** ~addState~

#+begin_src c :noweb-ref DescriptionBody

NAMESPACE_Description addState(string name, State aState)
{
  return add(name, new("NAMESPACE_Description").
             addInt("sprite", aState.sprite).
             addInt("frame", aState.Frame).compose());
}
#+end_src
#+begin_src c :noweb-ref TestsBody
{
  let d = new("Description");
  let state = players[consolePlayer].ReadyWeapon.FindState("Fire");
  d.addState("s", state);
  string expected = string.format("s: sprite: %d, frame: %d",
                                  state.sprite,
                                  state.Frame);
  it("description: state", Assert(d.compose() == expected));
}
#+end_src

** ~AsciiTable~

#+begin_src c :tangle ../build/StringUtils/StringUtils.zs

class NAMESPACE_AsciiTable
{
  enum AsciiTable
  {
    CHARACTER_NULL = 0,
    <<generate-table()>>
  }
}
#+end_src
#+name: generate-table
#+begin_src elisp
(defun number-to-ascii-enum (number)
  (format "%s = %d" (string-replace "(" ""
                    (string-replace ")" ""
                    (string-replace "-" "_"
                    (string-replace " " "_" (char-to-name number))))) number))

(mapconcat 'number-to-ascii-enum (number-sequence 1 127) ",\n")
#+end_src
#+begin_src c :noweb-ref TestsBody
{
  it("ASCII tab",
     Assert("\t" == string.format("%c", NAMESPACE_AsciiTable.CHARACTER_TABULATION)));

  it("ASCII \\n",
     Assert("\n" == string.format("%c", NAMESPACE_AsciiTable.LINE_FEED_LF)));
}
#+end_src

* Tests :noexport:

#+begin_src c :tangle ../build/StringUtils/zscript.zs

version 4.12.2

#include "StringUtils.zs"
#+end_src

#+begin_src c :tangle ../build/StringUtilsTest/zscript.zs

version 4.12.2

class su : NAMESPACE_su {}
class Description : NAMESPACE_Description {}

class su_Test : Clematis
{
  override void TestSuites()
  {
    Describe("StringUtils tests");
    <<TestsBody>>
    EndDescribe();
  }
}
#+end_src

#+begin_src text :tangle ../build/StringUtilsTest/commands.txt

wait 2; map map01; wait 2; netevent test:su_Test; wait 2; quit
#+end_src

#+begin_src elisp :exports none
(compile "cd ..; scons -Q StringUtilsTest")
#+end_src
