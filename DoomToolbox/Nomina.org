# SPDX-FileCopyrightText: © 2025 Alexander Kromm <mmaulwurff@gmail.com>
# SPDX-License-Identifier: GPL-3.0-only
:properties:
:header-args: :comments no :mkdirp yes :noweb yes :results none
:end:
#+title: Nomina v2.0.0-beta

* [[file:../docs/WhereAreTheProjectFiles.org][Where are the project files?]]

* Nomina
Nomina is a GZDoom add-on that helps with enemy names (tags).

Features:
- Automatic tags: if an enemy doesn't have a tag, Nomina assigns to beautify its class name and use it is a tag.
- In-game enemy tag correction: use ~na_rename <Class> <NewName>~ console command to assign a tag to a class manually.
- Tag databases: load ~na_data.json~ JSON lump (file) that contains class-tag pairs. See examples in tests below.

* License :noexport:
[[file:../LICENSES/GPL-3.0-only.txt][GPL-3.0-only]]
#+name: GPL
#+begin_src txt :exports none
SPDX-FileCopyrightText: © 2025 Alexander Kromm <mmaulwurff@gmail.com>
SPDX-License-Identifier: GPL-3.0-only
#+end_src

#+begin_src c :tangle ../build/Nomina/zscript.zs :exports none
// <<GPL>>
#+end_src
#+begin_src c :tangle ../build/NominaTest/zscript.zs :exports none
// <<GPL>>
#+end_src
#+begin_src c :tangle ../build/NominaTest/zscript/nat_Actors.zs :exports none
// <<GPL>>
#+end_src

[[file:../LICENSES/CC0-1.0.txt][CC0-1.0]]
#+name: CC
#+begin_src txt :exports none
SPDX-FileCopyrightText: © 2025 Alexander Kromm <mmaulwurff@gmail.com>
SPDX-License-Identifier: CC0-1.0
#+end_src

#+begin_src txt :tangle ../build/Nomina/cvarinfo.txt :exports none
// <<CC>>
#+end_src
#+begin_src txt :tangle ../build/Nomina/keyconf.txt :exports none
// <<CC>>
#+end_src
#+begin_src ini :tangle ../build/Nomina/language.txt :exports none
// <<CC>>
#+end_src
#+begin_src txt :tangle ../build/Nomina/mapinfo.txt :exports none
// <<CC>>
#+end_src
#+begin_src txt :tangle ../build/NominaTest/mapinfo.txt :exports none
// <<CC>>
#+end_src

* Strings
#+begin_src ini :tangle ../build/Nomina/language.txt
[enu default]
NA_USAGE = "Usage";

[ru]
NA_USAGE = "Использование";
#+end_src

* Tests begin
#+begin_src c :tangle ../build/NominaTest/zscript.zs
version 4.14

#include "zscript/nat_Actors.zs"

class nat_Test : Clematis {}

class nat_EventHandler : StaticEventHandler
{
  override void worldLoaded(WorldEvent event)
  {
    mTest = new ("nat_Test");
    mTest.Describe("Nomina tests");

    mExpectedNames = Dictionary.create();
    // continued elsewhere.
#+end_src

* ZScript
#+begin_src c :tangle ../build/Nomina/zscript.zs
version 4.14
#+end_src

#+begin_src txt :tangle ../build/Nomina/mapinfo.txt
GameInfo { EventHandlers = "na_EventHandler" }
#+end_src

#+begin_src txt :tangle ../build/Nomina/cvarinfo.txt
server nosave string na_config = "";
#+end_src

#+begin_src txt :tangle ../build/Nomina/keyconf.txt
Alias na_rename "netevent na_rename:%1:%2"
#+end_src

#+begin_src c :tangle ../build/Nomina/zscript.zs
class na_EventHandler : StaticEventHandler
{
  // Renames the spawned thing, if needed.
  override void worldThingSpawned(WorldEvent event)
  {
    Actor thing = event.thing;

    if (thing == NULL || !(thing.bIsMonster || thing is "Weapon")) return;

    {
      let[found, name] = findNameInConfig(thing.getClassName());
      if (found)
      {
        thing.setTag(name);
        return;
      }
    }
    {
      let[found, name] = findNameInData(thing.getClassName());
      if (found)
      {
        thing.setTag(name);
        return;
      }
    }

    bool hasTag = thing.getTag(".") != ".";
    if (hasTag) return;
#+end_src
#+begin_src c :tangle ../build/NominaTest/zscript/nat_Actors.zs
class nat_Imp : DoomImp {}
#+end_src
#+begin_src c :tangle ../build/NominaTest/zscript.zs
    mExpectedNames.insert("DoomImp", "Imp");
    mExpectedNames.insert("nat_Imp", "Imp");
#+end_src
#+begin_src c :tangle ../build/Nomina/zscript.zs

    string name = makeAutoName(thing.getClassName());
    thing.setTag(name);
  }

  // Handles `na_rename` command.
  override void networkProcess(ConsoleEvent event)
  {
    Array<string> parts;
    event.name.split(parts, ":");

    if (parts.size() == 0 || parts[0] != "na_rename") return;

    string className = parts[1];
    string newTag    = parts[2];

    if (parts.size() != 3 || className.length() == 0 || newTag.length() == 0)
    {
      Console.printf("%s:\nna_rename ClassName NewTag",
                     StringTable.localize("$NA_USAGE"));
      return;
    }

    let config = Dictionary.fromString(na_config);
    config.insert(className, newTag);
    CVar.getCvar("na_config").setString(config.toString());

    let i = ThinkerIterator.create(className);
    for (Actor anActor = Actor(i.next()); anActor != NULL; anActor = Actor(i.next()))
      anActor.setTag(newTag);
  }

  /// Turns '_' to spaces, capitalizes words, trims and removes duplicate spaces,
  /// splits camelCase.
  private static string makeAutoName(string className)
  {
    className.replace("_", " ");

    Array<string> words;
    className.split(words, " ", TOK_SkipEmpty);

    string result;

    foreach (word : words)
    {
      // It seems that non-latin characters are not allowed in class names. Overkill?
      let[firstChar, firstLength] = word.getNextCodepoint(0);

      string split = string.format("%c", string.charUpper(firstChar));
      for (uint i = firstLength; i < word.length();)
      {
        let[letter, next] = word.getNextCodepoint(i);

        if (string.charLower(letter) != letter) split.appendFormat(" %c", letter);
        else split.appendFormat("%c", letter);

        i = next;
      }

      if (result.length() != 0) result.appendFormat(" %s", split);
      else result.appendFormat("%s", split);
    }

    return result;
  }
#+end_src
#+begin_src c :tangle ../build/NominaTest/zscript/nat_Actors.zs
class _nat__nameless1_   : nat_Monster {}
class nat_CamelCaseEnemy : nat_Monster {}
#+end_src
#+begin_src c :tangle ../build/NominaTest/zscript.zs
    mExpectedNames.insert("_nat__nameless1_", "Nat Nameless1");
    mExpectedNames.insert("nat_CamelCaseEnemy", "Nat Camel Case Enemy");
#+end_src
#+begin_src c :tangle ../build/Nomina/zscript.zs
  private static bool, string findNameInConfig(string className)
  {
    let config    = Dictionary.fromString(na_config);
    string newTag = config.at(className);

    return newTag.length() != 0, newTag;
  }
#+end_src
#+begin_src c :tangle ../build/NominaTest/zscript/nat_Actors.zs
class nat_NamelessByConfig : nat_Monster {}
#+end_src
#+begin_src c :tangle ../build/NominaTest/zscript.zs
    // Cannot set a string value with " in the console, have to do it programmatically.
    CVar.getCvar("na_config").setString(
      "{\"nat_NamelessByConfig\":\"TestName\", \"Zombieman\":\"TestZombie\"}");
    mExpectedNames.insert("nat_NamelessByConfig", "TestName");
    mExpectedNames.insert("Zombieman", "TestZombie");
#+end_src
#+begin_src c :tangle ../build/Nomina/zscript.zs
  private bool, string findNameInData(string className)
  {
    string newTag = mData.at(className);

    return newTag.length() != 0, newTag;
  }

  // Initializes the event handler.
  override void OnEngineInitialize()
  {
    mData = Dictionary.create();

    string dataLump = "na_data";
    for (int i = Wads.findLump(dataLump, 0, Wads.AnyNamespace);
         i != -1;
         i = Wads.findLump(dataLump, i + 1, Wads.AnyNamespace))
    {
      let data = Dictionary.fromString(Wads.readLump(i));
      for (let i = DictionaryIterator.create(data); i.next();)
      {
        mData.insert(i.key(), i.value());
      }
    }
  }

  private Dictionary mData;
}
#+end_src
#+begin_src c :tangle ../build/NominaTest/zscript/nat_Actors.zs
class nat_NamelessByData1: nat_Monster {}
class nat_NamelessByData2: nat_Monster {}
class nat_NamelessByData3: nat_Monster {}
class nat_NamelessByData4: nat_Monster {}
#+end_src
#+begin_src js :tangle ../build/NominaTest/1/na_data.json
{
  "nat_NamelessByData1": "TestData1",
  "nat_NamelessByData3": "TestData3"
}
#+end_src
#+begin_src js :tangle ../build/NominaTest/na_data.json
{
  "nat_NamelessByData2": "TestData2",
  "nat_NamelessByData3": "TestData3-2",
  "nat_NamelessByData4": "TestData4"
}
#+end_src
#+begin_src js :tangle ../build/na_data.json
{
  "nat_NamelessByData4": "TestData4-2"
}
#+end_src
#+begin_src c :tangle ../build/NominaTest/zscript.zs
    mExpectedNames.insert("nat_NamelessByData1", "TestData1");
    mExpectedNames.insert("nat_NamelessByData2", "TestData2");
    mExpectedNames.insert("nat_NamelessByData3", "TestData3-2");
    mExpectedNames.insert("nat_NamelessByData4", "TestData4-2");
#+end_src

* Tests end
#+begin_src txt :tangle ../build/NominaTest/mapinfo.txt
GameInfo { EventHandlers = "nat_EventHandler" }
#+end_src

#+begin_src c :tangle ../build/NominaTest/zscript/nat_Actors.zs
class nat_Monster : Actor { Default { Monster; } }
#+end_src

#+begin_src c :tangle ../build/NominaTest/zscript.zs
    // started elsewhere.
    vector3 spawnPoint = players[consolePlayer].mo.pos + (100, 0, 0);

    for (let i = DictionaryIterator.create(mExpectedNames); i.next();)
      Actor.Spawn(i.key(), spawnPoint);
  }

  override void worldThingSpawned(WorldEvent event)
  {
    Actor thing = event.thing;

    if (thing == NULL || !(thing.bIsMonster || thing is "Weapon")) return;

    string className = thing.getClassName();
    if (mExpectedNames.at(className).length() == 0) return;

    bool isExpected = thing.getTag() == mExpectedNames.at(className);
    mTest.it(className, mTest.assert(isExpected));
    if (!isExpected)
      Console.printf("%s", thing.getTag());
  }

  override void OnUnregister() { mTest.EndDescribe(); }

  private Clematis mTest;
  private Dictionary mExpectedNames;
}
#+end_src

* Run tests
#+begin_src elisp
(load-file "../build/TestRunner/dt-scripts.el")
(dt-run-tests "../build/Nomina ../build/NominaTest ../build/na_data.json"
              "wait 2; map map01; wait 9; quit")
#+end_src
